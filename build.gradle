buildscript {
    ext.coroutine_version = "1.10.2"
    ext.dagger_hilt_version = "2.56.1"
    ext.gradle_version = '8.10.1'
    ext.kotlin_version = "2.1.21"
    ext.navigation_version = "2.9.0"
    ext.dokka_version = "2.0.0"
    ext.customFooterMessage = "© 2024 made with ❤️ by Nevis"
    ext.customLogoFile = projectDir.toString() + "/logo-style.css"

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$dagger_hilt_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigation_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

plugins {
    id 'org.jetbrains.dokka' version "${dokka_version}" apply true
}

ext.getConfig = { String name ->
    File localPropertiesFile = project.rootProject.file('local.properties')
    if (localPropertiesFile?.exists()) {
        Properties localProperties = new Properties()
        localProperties.load(localPropertiesFile.newDataInputStream())
        if (localProperties.containsKey(name)) {
            return localProperties.getProperty(name)
        }
    }
    def env = System.getenv(name)
    if (env != null) {
        return env
    }
    def prop = System.getProperty(name)
    if (prop != null) {
        return prop
    }
    if (project.hasProperty(name)) {
        return project.getProperty(name)
    }

    println("Getting env variable failed, returning empty: set $name as environment variable or as system property in your ~/.gradle/gradle.properties")
    return ""
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            url "https://maven.pkg.github.com/nevissecurity/nevis-mobile-authentication-sdk-android-package"
            credentials {
                username = getConfig("GH_USERNAME")
                password = getConfig("GH_PERSONAL_ACCESS_TOKEN")
            }
        }
        google {
            content {
                excludeGroupByRegex "ch\\.nevis\\..*"
            }
        }
        mavenCentral {
            content {
                excludeGroupByRegex "ch\\.nevis\\..*"
            }
        }
    }
}

dokka {
    dokkaPublications.html {
        outputDirectory.set(file("build/dokka/${project.name}"))
        failOnWarning.set(false)
        suppressInheritedMembers.set(true)
        suppressObviousFunctions.set(true)
    }

    pluginsConfiguration.html {
        customStyleSheets.from(customLogoFile)
        footerMessage.set(customFooterMessage)
    }
}

dependencies {
    dokka(project(":app"))
}
